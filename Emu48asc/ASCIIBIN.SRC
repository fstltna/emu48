!NO CODE
!RPL
::
  CK1NoBlame
  CK&DISPATCH1
  THREE
  ::
    DUP
    ELEVEN
    1_#1-SUB$
    "%%HP:"
    ONE
    POS$
    DUP
    #0=?SKIP
    ::
      #5+
      LAST$
      DUP
      ";"
      ONE
      POS$
      2DUP
      #1+
      LAST$
      UNROT
      1_#1-SUB$
      DUPLEN$
      THIRTEEN #=
      ?SKIP
      SETSIZEERR
      RCLSYSF
      UNROT
      DUP
      FOUR
      SUB$1#
      FORTYEIGHT
      #-
      UNCOERCE
      DOTRANSIO
      DUP
      EIGHT
      SUB$1#
      DUP
      #52
      #=
      ITE
      ::
        SEVENTEEN
        SetSysFlag
        DROP
      ;
      ::
        SEVENTEEN
        ClrSysFlag
        #47
        #=
        ITE
        ::
          EIGHTEEN
          SetSysFlag
        ;
        ::
          EIGHTEEN
          ClrSysFlag
        ;
      ;
      TWELVE
      SUB$1#
      FORTYFOUR
      #=
      ITE
      ::
        FIFTYONE
        SetSysFlag
      ;
      ::
        FIFTYONE
        ClrSysFlag
      ;
    ;
    KINVISLF
    DROP
    SWAP
    DOSTOSYSF
    palparse
    NOTcase2DROP
    DUP
    CARCOMP
    ' xSILENT'
    EQ
    NOT?SEMI
    TWO
    NTHCOMPDROP
  ;
  ZERO
  ::
    EDITDECOMP$
    KVISLF
    "%%HP: T("
    GetIOPAR
    6UNROLL 5DROP
    IOCheckReal %IP>#
    DUP FOUR #<
    ?SKIP
    SetIOPARErr
    #>$
    !append$
    ")A("
    !append$
    SEVENTEEN
    TestSysFlag
    ITE
    CHR_R
    ::
      EIGHTEEN
      TestSysFlag
      ITE
      CHR_G
      CHR_D
    ;
    CHR>$
    !append$
    FIFTYONE
    TestSysFlag
    ITE
    ")F(,)"
    ")F(.)"
    !append$
    ";\0D\0A"
    !append$SWAP  
    !append$
  ;
;
@